function(cpprest_find_botan)
  if(TARGET cpprestsdk_botan_internal)
    return()
  endif()

  INCLUDE (FindPackageHandleStandardArgs)

  SET (_BOTAN_POSSIBLE_DIRS ${BOTAN_ROOT_DIR})

  IF (WIN32)
    SET (_BOTAN_POSSIBLE_DIRS ${_BOTAN_POSSIBLE_DIRS} "C:\\usr\\local" "C:\\usr")
  ENDIF (WIN32)

  # Note: after 1.11.34, botan library moved to semantic versioning (a.k.a. 2.0)
  SET (_BOTAN_POSSIBLE_INCLUDE_SUFFIXES include include/botan-1.10 include/botan-1.11 include/botan-2.0 include/botan-2)
  SET (_BOTAN_POSSIBLE_LIB_SUFFIXES lib)

  FIND_PATH (BOTAN_ROOT_DIR
    NAMES botan/botan.h
    PATHS ${_BOTAN_POSSIBLE_DIRS}
    PATH_SUFFIXES ${_BOTAN_POSSIBLE_INCLUDE_SUFFIXES}
    DOC "Botan root directory")

  FIND_PATH (BOTAN_INCLUDE_DIR
    NAMES botan/botan.h
    PATHS ${BOTAN_ROOT_DIR}
    PATH_SUFFIXES ${_BOTAN_POSSIBLE_INCLUDE_SUFFIXES}
    DOC "Botan include directory")

  FIND_LIBRARY (BOTAN_LIBRARY_DEBUG
    NAMES botand botand-1.10 botand-1.11 botand-2
    PATHS ${_BOTAN_POSSIBLE_DIRS}
    PATH_SUFFIXES ${_BOTAN_POSSIBLE_LIB_SUFFIXES}
    DOC "Botan debug library")

  FIND_LIBRARY (BOTAN_LIBRARY_RELEASE
    NAMES botan botan-1.10 botan-1.11 botan-2
    PATHS ${_BOTAN_POSSIBLE_DIRS}
    PATH_SUFFIXES ${_BOTAN_POSSIBLE_LIB_SUFFIXES}
    DOC "Botan release library")

  IF (NOT DEFINED BOTAN_LIBRARIES)
    IF (BOTAN_LIBRARY_DEBUG AND BOTAN_LIBRARY_RELEASE)
      SET (BOTAN_LIBRARIES
        optimized ${BOTAN_LIBRARY_RELEASE}
        debug ${BOTAN_LIBRARY_DEBUG})
    ELSEIF (BOTAN_LIBRARY_RELEASE)
      SET (BOTAN_LIBRARIES ${BOTAN_LIBRARY_RELEASE})
    ENDIF (BOTAN_LIBRARY_DEBUG AND BOTAN_LIBRARY_RELEASE)
  ENDIF (NOT DEFINED BOTAN_LIBRARIES)

  IF (BOTAN_INCLUDE_DIR)
    SET (BOTAN_INCLUDE_DIRS ${BOTAN_INCLUDE_DIR})

    SET (_BOTAN_VERSION_HEADER ${BOTAN_INCLUDE_DIR}/botan/build.h)

    IF (EXISTS ${_BOTAN_VERSION_HEADER})
      FILE (STRINGS ${_BOTAN_VERSION_HEADER} _BOTAN_VERSION_TMP REGEX
        "#define BOTAN_VERSION_(MAJOR|MINOR|PATCH)[ \t]+[0-9]+")

      STRING (REGEX REPLACE
        ".*#define BOTAN_VERSION_MAJOR[ \t]+([0-9]+).*" "\\1" BOTAN_VERSION_MAJOR
        ${_BOTAN_VERSION_TMP})
      STRING (REGEX REPLACE
        ".*#define BOTAN_VERSION_MINOR[ \t]+([0-9]+).*" "\\1" BOTAN_VERSION_MINOR
        ${_BOTAN_VERSION_TMP})
      STRING (REGEX REPLACE
        ".*#define BOTAN_VERSION_PATCH[ \t]+([0-9]+).*" "\\1" BOTAN_VERSION_PATCH
        ${_BOTAN_VERSION_TMP})

      SET (BOTAN_VERSION_COUNT 3)
      SET (BOTAN_VERSION
        ${BOTAN_VERSION_MAJOR}.${BOTAN_VERSION_MINOR}.${BOTAN_VERSION_PATCH})
    ENDIF (EXISTS ${_BOTAN_VERSION_HEADER})
  ENDIF (BOTAN_INCLUDE_DIR)

  MARK_AS_ADVANCED (BOTAN_ROOT_DIR BOTAN_INCLUDE_DIR BOTAN_LIBRARY_DEBUG
    BOTAN_LIBRARY_RELEASE)

  FIND_PACKAGE_HANDLE_STANDARD_ARGS (Botan REQUIRED_VARS BOTAN_INCLUDE_DIRS
    BOTAN_LIBRARIES VERSION_VAR BOTAN_VERSION)

  add_library(Botan INTERFACE)
  target_include_directories(Botan INTERFACE ${BOTAN_INCLUDE_DIR})
  target_link_libraries(Botan INTERFACE ${BOTAN_LIBRARIES})

  add_library(cpprestsdk_botan_internal INTERFACE)
  target_link_libraries(cpprestsdk_botan_internal INTERFACE "$<BUILD_INTERFACE:${BOTAN_LIBRARY}>")
  target_include_directories(cpprestsdk_botan_internal INTERFACE "$<BUILD_INTERFACE:${BOTAN_INCLUDE_DIR}>")
endfunction()
